#!/usr/bin/env just --justfile

set quiet := true
set positional-arguments := true

CLUSTER_DIR := "."
BOOTSTRAP_DIR := CLUSTER_DIR + "/bootstrap"
KUBECONFIG := "$(pwd)/kubeconfig"
TALOS_CONTROLLER := "$(talosctl config info --output json | jq --raw-output '.endpoints[]' | shuf -n 1)"
CLUSTER_NAME := "dextek"

_default:
    just --list

# Bootstrap all resources
all: && _cleanup
    just tofu
    just talosconfig
    just talos
    just kubeconfig
    just readyCheck
    just helmfile
    just wipe
    just k9s

tofu:
    #!/usr/bin/env bash
    set -euo pipefail
    cd "{{ CLUSTER_DIR }}/terraform/talos"
    echo "Running Tofu..."
    tofu init --upgrade
    tofu apply --auto-approve --var talosconfig=true
    mkdir -p ~/.talos/configs
    cp talosconfig ~/.talos/configs/{{ CLUSTER_NAME }}

talosconfig:
    #!/usr/bin/env bash
    set -euo pipefail
    talswitcher ctx {{ CLUSTER_NAME }}

talos:
    #!/usr/bin/env bash
    TALOS_CONTROLLER="{{ TALOS_CONTROLLER }}"
    printf "Bootstrapping Node: $TALOS_CONTROLLER..."
    until talosctl --nodes $TALOS_CONTROLLER bootstrap > /dev/null 2>&1; do sleep 5; done
    echo " Done"

kubeconfig:
    printf "Getting kubeconfig..."
    until talosctl kubeconfig -n {{ TALOS_CONTROLLER }} --force > /dev/null 2>&1; do sleep 3; done
    kubectl config set-cluster {{ CLUSTER_NAME }} --server "https://{{ TALOS_CONTROLLER }}:6443"
    echo " Done"

readyCheck:
    echo "Waiting for Nodes to be ready..."
    until kubectl wait nodes --for=condition=Ready=False --all --timeout=10m > /dev/null 2>&1; do sleep 5; done

namespaces:
    #!/usr/bin/env bash
    set -euo pipefail
    echo "Applying namespaces..."
    find apps -mindepth 1 -maxdepth 1 -type d | while IFS= read -r dir; do
        if ! kustomize build "$dir" | yq ea -e 'select(.kind == "Namespace")' | kubectl apply --server-side --field-manager bootstrap --force-conflicts -f - 2>/dev/null; then
            echo "No namespace found in $(basename "$dir") or already applied"
        fi
    done
    echo " Done"

crds:
    printf "Applying CRDs..."
    until helmfile -f {{ BOOTSTRAP_DIR }}/helmfile.d/00-crds.yaml template -q | yq ea -e 'select(.kind == "CustomResourceDefinition")' | kubectl apply --server-side --field-manager bootstrap --force-conflicts -f - > /dev/null 2>&1; do sleep 3; done
    echo " Done"

apps:
    printf "Deploying apps via helmfile..."
    until helmfile -f {{ BOOTSTRAP_DIR }}/helmfile.d/01-apps.yaml sync --hide-notes; do sleep 3; done
    echo " Done"

helmfile: namespaces crds apps
    printf "Applying Flux secrets..."
    infisical export --template={{ BOOTSTRAP_DIR }}/resources.yaml.tmpl | kubectl apply -f -
    echo " Done"

wipe:
    #!/usr/bin/env bash
    NODES=$(talosctl config info --output json | jq --raw-output '.endpoints[]' | tr '\n' ' ' | awk '{$1=$1};1')
    echo "Wiping disks on Nodes: $NODES"
    for node in $NODES; do
      echo "Waiting for Node to be ready: $node"
      until talosctl health -n $node >/dev/null 2>&1; do sleep 3; done
      echo "Wiping disks on: $node"
      DISKS=$(talosctl get disks --output json -n $node | jq -r '. | select(.spec.dev_path | contains("nvme")) | .spec.dev_path' | sed 's|/dev/||g' | tr '\n' ' ' | awk '{$1=$1};1')
      for disk in $DISKS; do
        printf "Wiping: $disk"; sleep 1;
        if talosctl wipe disk $disk -n $node >/dev/null 2>&1; then
          echo " SUCCESS";
        else
          echo " FAILED (Probably OS Drive)";
        fi
      done;
    done

k9s:
    k9s --all-namespaces --command pods

_cleanup:
    rm -f {{ KUBECONFIG }}
